---
title: Using an assertions library
description: Hardhat 3 Tutorial - Using an assertions library
sidebar:
  order: 4
---

import Install from "@hh/Install.astro";
import Run from "@hh/Run.astro";

The tests you've written so far are simple. They call functions and check that the contract's state is what you expect. But what if you want to do something more complex, like checking that the correct event was emitted? Solidity doesn't have a built-in way to do that, so you'll need to use an assertions library.

Let's install the `forge-std` library, which provides useful testing utilities:

<Install packages={`"foundry-rs/forge-std#v1.11.0"`} />

Once it's installed, let's modify `Counter.t.sol` to use `forge-std`:

```diff lang=solidity collapse={11-27}
// contracts/Counter.t.sol
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.28;

import { Counter} from "./Counter.sol";
+import { Test } from "forge-std/Test.sol";

-contract CounterTest {
+contract CounterTest is Test {
    Counter counter;

    function setUp() public {
      counter = new Counter();
    }

    function test_InitialValueIsZero() public view {
        require(counter.x() == 0, "x should start at 0");
    }

    function test_IncIncreasesByOne() public {
        counter.inc();
        require(counter.x() == 1, "inc should increase x by 1");
    }

    function test_IncByIncreasesByGivenAmount() public {
        counter.incBy(3);
        require(counter.x() == 3, "incBy should increase x by the given amount");
    }
}
```

Now let's add a new test that checks that the correct event is emitted when we call `inc`:

```solidity collapse={1-5,10-26} ins={28-33}
// contracts/Counter.t.sol
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.28;

import { Counter } from "./Counter.sol";
import { Test } from "forge-std/Test.sol";

contract CounterTest is Test {
  Counter counter;

  function setUp() public {
    counter = new Counter();
  }

  function test_InitialValueIsZero() public view {
    require(counter.x() == 0, "x should start at 0");
  }

  function test_IncIncreasesByOne() public {
    counter.inc();
    require(counter.x() == 1, "inc should increase x by 1");
  }

  function test_IncByIncreasesByGivenAmount() public {
    counter.incBy(3);
    require(counter.x() == 3, "incBy should increase x by the given amount");
  }

  function test_IncEmitsIncrementEvent() public {
    vm.expectEmit();
    emit Counter.Increment(1);

    counter.inc();
  }
}
```

Here, the combination of `vm.expectEmit()` and `emit Increment(1);` tells the test framework that the next call (`counter.inc()`) should emit an `Increment` event with the argument `1`. The test will fail if the event isn't emitted or if it's emitted with different arguments.

:::note
In the previous example, `vm.expectEmit()` is a **cheatcode**.

Cheatcodes are special functions that manipulate the runtime environment of your tests. They're only available in the context of tests and aren't part of the Solidity language itself.

You can see a full list of cheatcodes supported by `forge-std` <a href="https://getfoundry.sh/reference/cheatcodes/overview" target="_blank" rel="noopener noreferrer">here</a>.
:::

Run the tests again to verify that everything works:

<Run command="hardhat test solidity" />
